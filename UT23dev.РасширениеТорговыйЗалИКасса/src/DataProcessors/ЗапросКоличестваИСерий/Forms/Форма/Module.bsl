
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура РТЗ_ПриОткрытииПосле(Отказ)
	
	Если РТЗ_ЭтотПользовательПринадлежитКРасширению() Тогда
		
		Элементы.ГруппаКоличество.Видимость = Ложь;
		РТЗ_ИзменитьЗначениеКоличество(0);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РТЗ_ОКВместо(Команда)
	
	ОчиститьСообщения();
	
	Если РТЗ_ЭтотПользовательПринадлежитКРасширению() Тогда //Значит проверяем и возвращаем по нашему
		
		Если Не РТЗ_СоответсвуютЛиЗначенияПодобраноДоступно() Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Подобрано не может превышать текущую доступность на складе'"),,
				Элементы.ТоварыПодобрано,, 
				Истина);
			Возврат;
		КонецЕсли;
		
		РТЗ_ИтогПодобрано = Объект.Товары.Итог("Подобрано");
		Если РТЗ_ИтогПодобрано > 0 Тогда
			
			Объект.Количество = РТЗ_ИтогПодобрано;
			
			РТЗ_ПодобранныйТовар = РТЗ_ПолучитьПодобранныеТоварыБезПустыхСсылок(ПодобранныеТовары());
			Закрыть(Новый Структура("ПодобранныеТовары", РТЗ_ПодобранныйТовар));
			
		Иначе	
			Отмена(Неопределено);
		КонецЕсли;
		
	Иначе	
		ПродолжитьВызов();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получить подобранные товары без пустых ссылок.
// 
// Параметры:
//  МассивТоваров - Массив
//  	
// 
// Возвращаемое значение:
//  - Массив
&НаСервере
Функция РТЗ_ПолучитьПодобранныеТоварыБезПустыхСсылок(МассивТоваров)
	
	РТЗ_МассивРезультат = Новый Массив;
	
	Для Каждого СтрокаТовар Из МассивТоваров Цикл
		
		Если Не СтрокаТовар.Склад = Справочники.Склады.ПустаяСсылка() Тогда
			РТЗ_МассивРезультат.Добавить(СтрокаТовар);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РТЗ_МассивРезультат;
	
КонецФункции

// Количество соответсвует подобрано в таблице товар.
// 
// Возвращаемое значение:
//  Булево - Количество соответсвует подобрано в таблице товар
&НаСервере
Функция РТЗ_СоответсвуютЛиЗначенияПодобраноДоступно()

	Для Каждого СтрокаТовары Из Объект.Товары Цикл
		
		Если СтрокаТовары.ВНаличии < СтрокаТовары.Подобрано Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

// Этот пользователь принадлежит к расширению.
// 
// Возвращаемое значение:
//  Булево - Этот пользователь принадлежит к расширению
&НаСервере
Функция РТЗ_ЭтотПользовательПринадлежитКРасширению()
	
	Возврат Пользователи.РолиДоступны("РТЗ_БазовыеПраваТорговыйЗал");
	
КонецФункции

// РТЗ изменить значение количество.
// 
// Параметры:
//  ИтогПодобрано - Число - Итог подобрано
&НаСервере
Процедура РТЗ_ИзменитьЗначениеКоличество(ИтогПодобрано = Неопределено)
	
	Если Не ЗначениеЗаполнено(ИтогПодобрано) Тогда
		ИтогПодобрано = Объект.Товары.Итог("Подобрано");
	КонецЕсли;
	
	Объект.Количество = ИтогПодобрано;
	Объект.КоличествоУпаковок = ИтогПодобрано;
	
КонецПроцедуры

&НаКлиенте
Процедура РТЗ_ИзменитьЗначениеКоличествоНаКлиенте(ИтогПодобрано = Неопределено)
	
	РТЗ_ИзменитьЗначениеКоличество(ИтогПодобрано);
	
КонецПроцедуры

#КонецОбласти
